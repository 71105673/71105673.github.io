---
title: "VGA Day-1"
date: "2025-09-08"
thumbnail: "../../../assets/img/VGA/image.png"
---

# 🎨 VGA (Video Graphics Array) 정리

## 📌 기본 개요
- **VGA**: 1987년 IBM에서 개발한 그래픽 표준  
- **해상도**: 640 × 480 (표준 VGA)  
- **색상**: 최대 256색 (8-bit 팔레트), 기본 16색  
- **주파수**: 60 Hz 동작 (표준)  

---

## 🖥️ VGA 신호 구성
VGA는 **아날로그 신호** 기반으로, 총 5가지 주요 신호가 필요함:

- **R, G, B (Red, Green, Blue)** : 각 색상의 아날로그 값 (0~0.7V)  
- **HSYNC (Horizontal Sync)** : 수평 동기 신호  
- **VSYNC (Vertical Sync)** : 수직 동기 신호  

---

## ⏱️ VGA 타이밍 (640×480 @ 60Hz 기준)
- **픽셀 클럭**: 25.175 MHz  
- **프레임 주파수**: 60 Hz  
- **라인 수**: 525 라인 (480 표시 + 동기 + 블랭크)  
- **픽셀 수/라인**: 800 픽셀 (640 표시 + 동기 + 블랭크) 
-  
![alt text](<../../../assets/img/VGA/day-1/스크린샷 2025-09-08 105245.png>)
![alt text](<../../../assets/img/VGA/day-1/스크린샷 2025-09-08 105853.png>)
![alt text](<스크린샷 2025-09-08 112739.png>)
전압구동을 통해 밝기를 조절한다 (RGB 픽셀 조절)

---

## 🔑 동작 원리
1. **픽셀 클럭**에 맞춰 R/G/B 신호 출력  
2. 매 라인의 끝에서 **HSYNC** 발생  
3. 매 프레임의 끝에서 **VSYNC** 발생  
4. 모니터는 HSYNC + VSYNC를 기준으로 프레임 재구성  

---

## 🛠️ 활용 예시
- **FPGA 프로젝트**: 간단한 도형/문자 출력, 게임 구현 (예: Pong, Snake)  
- **임베디드 시스템**: 저해상도 디스플레이 출력  

## VGA 기본 해상도
![alt text](<../../../assets/img/VGA/day-1/스크린샷 2025-09-08 103119.png>)

해당 부분의 굴곡진 부분 -> 편광 강도를 나타냄

### CRT -> 브라운관의 기초
![alt text](<../../../assets/img/VGA/day-1/스크린샷 2025-09-08 095209.png>)


## Block Diagram
![alt text](<../../../assets/img/VGA/day-1/스크린샷 2025-09-08 113702.png>)

-> Negative, positive 고려해서 값을 설정하기
![alt text](<../../../assets/img/VGA/day-1/스크린샷 2025-09-08 122002.png>)


## VGA_Decoder.sv
```verilog
`timescale 1ns / 1ps

module VGA_Decoder (
    input  logic       clk,
    input  logic       reset,
    output logic       h_sync,
    output logic       v_sync,
    output logic [9:0] x_pixel,
    output logic [9:0] y_pixel,
    output logic       DE
);
    logic pclk;
    logic [9:0] v_counter;
    logic [9:0] h_counter;

    Pixel_clk_gen U_P_CLK (.*);
    Pixel_counter U_P_COUNTER (.*);
    vga_decoder U_VGA_DECODER (.*);
endmodule

///////////////////////////////////////////////////////////////

module Pixel_clk_gen (
    input  logic clk,
    input  logic reset,
    output logic pclk
);
    logic [1:0] p_counter;

    always_ff @(posedge clk, posedge reset) begin
        if (reset) begin
            p_counter <= 0;
        end else begin
            if (p_counter == 3) begin
                p_counter <= 0;
                pclk      <= 1'b1;
            end else begin
                p_counter <= p_counter + 1;
                pclk      <= 1'b0;
            end
        end
    end
endmodule

///////////////////////////////////////////////////////////////

module Pixel_counter (
    input  logic       pclk,
    input  logic       reset,
    output logic [9:0] v_counter,
    output logic [9:0] h_counter
);
    localparam H_MAX = 800, V_MAX = 525;  // VGA Spec

    always_ff @(posedge pclk, posedge reset) begin
        if (reset) begin
            h_counter <= 0;
        end else begin
            if (h_counter == H_MAX - 1) begin
                h_counter <= 0;
            end else begin
                h_counter <= h_counter + 1;
            end
        end
    end

    always_ff @(posedge pclk, posedge reset) begin
        if (reset) begin
            v_counter <= 0;
        end else begin
            if (h_counter == H_MAX - 1) begin
                if (v_counter == V_MAX - 1) begin
                    v_counter <= 0;
                end else begin
                    v_counter <= v_counter + 1;
                end
            end
        end
    end
endmodule

///////////////////////////////////////////////////////////////

module vga_decoder (
    input  logic [9:0] h_counter,
    input  logic [9:0] v_counter,
    output logic       h_sync,
    output logic       v_sync,
    output logic [9:0] x_pixel,
    output logic [9:0] y_pixel,
    output logic       DE
);
    // Horizontal timing (frame)
    localparam H_Visible_area = 640;
    localparam H_Front_porch = 16;
    localparam H_Sync_pulse = 96;
    localparam H_Back_porch = 48;
    localparam H_Whole_line = 800;
    // Vertical timing (frame)
    localparam V_Visible_area = 480;
    localparam V_Front_porch = 10;
    localparam V_Sync_pulse = 2;
    localparam V_Back_porch = 33;
    localparam V_Whole_frame = 525;

    assign DE = (h_counter < H_Visible_area) && (v_counter < V_Visible_area);  // 사이 영역일 때 
    assign h_sync = !((h_counter >= (H_Visible_area + H_Front_porch)) && (h_counter < (H_Visible_area + H_Front_porch + H_Sync_pulse)));    // low
    assign v_sync = !((v_counter >= (V_Visible_area + V_Front_porch)) && (v_counter < (V_Visible_area + V_Front_porch + V_Sync_pulse)));    // low
    assign x_pixel = (h_counter < H_Visible_area) ? h_counter : 0;
    assign y_pixel = (v_counter < V_Visible_area) ? v_counter : 0;

endmodule
```

### RTL
![alt text](<../../../assets/img/VGA/day-1/스크린샷 2025-09-08 124436.png>)

## VGA_RGV_Switch.sv
```verilog
`timescale 1ns / 1ps

module VGA_RGB_Switch (
    input  logic [3:0] sw_red,
    input  logic [3:0] sw_green,
    input  logic [3:0] sw_blue,
    input  logic       DE,
    output logic [3:0] r_port,
    output logic [3:0] g_port,
    output logic [3:0] b_port
);
    assign r_port = DE ? sw_red : 4'b0;
    assign g_port = DE ? sw_green : 4'b0;
    assign b_port = DE ? sw_blue : 4'b0;
endmodule
```

## VGA_Display_Switch
```verilog
`timescale 1ns / 1ps

module VGA_Display_Switch (
    input  logic       clk,
    input  logic       reset,
    input  logic [3:0] sw_red,
    input  logic [3:0] sw_green,
    input  logic [3:0] sw_blue,
    output logic       h_sync,
    output logic       v_sync,
    output logic [3:0] r_port,
    output logic [3:0] g_port,
    output logic [3:0] b_port
);
    logic DE;

    VGA_Decoder U_VGA_DEC (
        .*,
        .x_pixel(),
        .y_pixel()
    );

    VGA_RGB_Switch U_VGA_RGB_SW (.*);
endmodule
```

### RTL
![alt text](<../../../assets/img/VGA/day-1/스크린샷 2025-09-08 125251.png>)
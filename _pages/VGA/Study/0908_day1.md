---
title: "VGA Day-1"
date: "2025-09-08"
thumbnail: "../../../assets/img/VGA/image.png"
---

# ğŸ¨ VGA (Video Graphics Array) ì •ë¦¬

## ğŸ“Œ ê¸°ë³¸ ê°œìš”
- **VGA**: 1987ë…„ IBMì—ì„œ ê°œë°œí•œ ê·¸ë˜í”½ í‘œì¤€  
- **í•´ìƒë„**: 640 Ã— 480 (í‘œì¤€ VGA)  
- **ìƒ‰ìƒ**: ìµœëŒ€ 256ìƒ‰ (8-bit íŒ”ë ˆíŠ¸), ê¸°ë³¸ 16ìƒ‰  
- **ì£¼íŒŒìˆ˜**: 60 Hz ë™ì‘ (í‘œì¤€)  

---

## ğŸ–¥ï¸ VGA ì‹ í˜¸ êµ¬ì„±
VGAëŠ” **ì•„ë‚ ë¡œê·¸ ì‹ í˜¸** ê¸°ë°˜ìœ¼ë¡œ, ì´ 5ê°€ì§€ ì£¼ìš” ì‹ í˜¸ê°€ í•„ìš”í•¨:

- **R, G, B (Red, Green, Blue)** : ê° ìƒ‰ìƒì˜ ì•„ë‚ ë¡œê·¸ ê°’ (0~0.7V)  
- **HSYNC (Horizontal Sync)** : ìˆ˜í‰ ë™ê¸° ì‹ í˜¸  
- **VSYNC (Vertical Sync)** : ìˆ˜ì§ ë™ê¸° ì‹ í˜¸  

---

## â±ï¸ VGA íƒ€ì´ë° (640Ã—480 @ 60Hz ê¸°ì¤€)
- **í”½ì…€ í´ëŸ­**: 25.175 MHz  
- **í”„ë ˆì„ ì£¼íŒŒìˆ˜**: 60 Hz  
- **ë¼ì¸ ìˆ˜**: 525 ë¼ì¸ (480 í‘œì‹œ + ë™ê¸° + ë¸”ë­í¬)  
- **í”½ì…€ ìˆ˜/ë¼ì¸**: 800 í”½ì…€ (640 í‘œì‹œ + ë™ê¸° + ë¸”ë­í¬) 
-  
![alt text](<../../../assets/img/VGA/day-1/ìŠ¤í¬ë¦°ìƒ· 2025-09-08 105245.png>)
![alt text](<../../../assets/img/VGA/day-1/ìŠ¤í¬ë¦°ìƒ· 2025-09-08 105853.png>)
![alt text](<ìŠ¤í¬ë¦°ìƒ· 2025-09-08 112739.png>)
ì „ì••êµ¬ë™ì„ í†µí•´ ë°ê¸°ë¥¼ ì¡°ì ˆí•œë‹¤ (RGB í”½ì…€ ì¡°ì ˆ)

---

## ğŸ”‘ ë™ì‘ ì›ë¦¬
1. **í”½ì…€ í´ëŸ­**ì— ë§ì¶° R/G/B ì‹ í˜¸ ì¶œë ¥  
2. ë§¤ ë¼ì¸ì˜ ëì—ì„œ **HSYNC** ë°œìƒ  
3. ë§¤ í”„ë ˆì„ì˜ ëì—ì„œ **VSYNC** ë°œìƒ  
4. ëª¨ë‹ˆí„°ëŠ” HSYNC + VSYNCë¥¼ ê¸°ì¤€ìœ¼ë¡œ í”„ë ˆì„ ì¬êµ¬ì„±  

---

## ğŸ› ï¸ í™œìš© ì˜ˆì‹œ
- **FPGA í”„ë¡œì íŠ¸**: ê°„ë‹¨í•œ ë„í˜•/ë¬¸ì ì¶œë ¥, ê²Œì„ êµ¬í˜„ (ì˜ˆ: Pong, Snake)  
- **ì„ë² ë””ë“œ ì‹œìŠ¤í…œ**: ì €í•´ìƒë„ ë””ìŠ¤í”Œë ˆì´ ì¶œë ¥  

## VGA ê¸°ë³¸ í•´ìƒë„
![alt text](<../../../assets/img/VGA/day-1/ìŠ¤í¬ë¦°ìƒ· 2025-09-08 103119.png>)

í•´ë‹¹ ë¶€ë¶„ì˜ êµ´ê³¡ì§„ ë¶€ë¶„ -> í¸ê´‘ ê°•ë„ë¥¼ ë‚˜íƒ€ëƒ„

### CRT -> ë¸Œë¼ìš´ê´€ì˜ ê¸°ì´ˆ
![alt text](<../../../assets/img/VGA/day-1/ìŠ¤í¬ë¦°ìƒ· 2025-09-08 095209.png>)


## Block Diagram
![alt text](<../../../assets/img/VGA/day-1/ìŠ¤í¬ë¦°ìƒ· 2025-09-08 113702.png>)

-> Negative, positive ê³ ë ¤í•´ì„œ ê°’ì„ ì„¤ì •í•˜ê¸°
![alt text](<../../../assets/img/VGA/day-1/ìŠ¤í¬ë¦°ìƒ· 2025-09-08 122002.png>)


## VGA_Decoder.sv
```verilog
`timescale 1ns / 1ps

module VGA_Decoder (
    input  logic       clk,
    input  logic       reset,
    output logic       h_sync,
    output logic       v_sync,
    output logic [9:0] x_pixel,
    output logic [9:0] y_pixel,
    output logic       DE
);
    logic pclk;
    logic [9:0] v_counter;
    logic [9:0] h_counter;

    Pixel_clk_gen U_P_CLK (.*);
    Pixel_counter U_P_COUNTER (.*);
    vga_decoder U_VGA_DECODER (.*);
endmodule

///////////////////////////////////////////////////////////////

module Pixel_clk_gen (
    input  logic clk,
    input  logic reset,
    output logic pclk
);
    logic [1:0] p_counter;

    always_ff @(posedge clk, posedge reset) begin
        if (reset) begin
            p_counter <= 0;
        end else begin
            if (p_counter == 3) begin
                p_counter <= 0;
                pclk      <= 1'b1;
            end else begin
                p_counter <= p_counter + 1;
                pclk      <= 1'b0;
            end
        end
    end
endmodule

///////////////////////////////////////////////////////////////

module Pixel_counter (
    input  logic       pclk,
    input  logic       reset,
    output logic [9:0] v_counter,
    output logic [9:0] h_counter
);
    localparam H_MAX = 800, V_MAX = 525;  // VGA Spec

    always_ff @(posedge pclk, posedge reset) begin
        if (reset) begin
            h_counter <= 0;
        end else begin
            if (h_counter == H_MAX - 1) begin
                h_counter <= 0;
            end else begin
                h_counter <= h_counter + 1;
            end
        end
    end

    always_ff @(posedge pclk, posedge reset) begin
        if (reset) begin
            v_counter <= 0;
        end else begin
            if (h_counter == H_MAX - 1) begin
                if (v_counter == V_MAX - 1) begin
                    v_counter <= 0;
                end else begin
                    v_counter <= v_counter + 1;
                end
            end
        end
    end
endmodule

///////////////////////////////////////////////////////////////

module vga_decoder (
    input  logic [9:0] h_counter,
    input  logic [9:0] v_counter,
    output logic       h_sync,
    output logic       v_sync,
    output logic [9:0] x_pixel,
    output logic [9:0] y_pixel,
    output logic       DE
);
    // Horizontal timing (frame)
    localparam H_Visible_area = 640;
    localparam H_Front_porch = 16;
    localparam H_Sync_pulse = 96;
    localparam H_Back_porch = 48;
    localparam H_Whole_line = 800;
    // Vertical timing (frame)
    localparam V_Visible_area = 480;
    localparam V_Front_porch = 10;
    localparam V_Sync_pulse = 2;
    localparam V_Back_porch = 33;
    localparam V_Whole_frame = 525;

    assign DE = (h_counter < H_Visible_area) && (v_counter < V_Visible_area);  // ì‚¬ì´ ì˜ì—­ì¼ ë•Œ 
    assign h_sync = !((h_counter >= (H_Visible_area + H_Front_porch)) && (h_counter < (H_Visible_area + H_Front_porch + H_Sync_pulse)));    // low
    assign v_sync = !((v_counter >= (V_Visible_area + V_Front_porch)) && (v_counter < (V_Visible_area + V_Front_porch + V_Sync_pulse)));    // low
    assign x_pixel = (h_counter < H_Visible_area) ? h_counter : 0;
    assign y_pixel = (v_counter < V_Visible_area) ? v_counter : 0;

endmodule
```

### RTL
![alt text](<../../../assets/img/VGA/day-1/ìŠ¤í¬ë¦°ìƒ· 2025-09-08 124436.png>)

## VGA_RGV_Switch.sv
```verilog
`timescale 1ns / 1ps

module VGA_RGB_Switch (
    input  logic [3:0] sw_red,
    input  logic [3:0] sw_green,
    input  logic [3:0] sw_blue,
    input  logic       DE,
    output logic [3:0] r_port,
    output logic [3:0] g_port,
    output logic [3:0] b_port
);
    assign r_port = DE ? sw_red : 4'b0;
    assign g_port = DE ? sw_green : 4'b0;
    assign b_port = DE ? sw_blue : 4'b0;
endmodule
```

## VGA_Display_Switch
```verilog
`timescale 1ns / 1ps

module VGA_Display_Switch (
    input  logic       clk,
    input  logic       reset,
    input  logic [3:0] sw_red,
    input  logic [3:0] sw_green,
    input  logic [3:0] sw_blue,
    output logic       h_sync,
    output logic       v_sync,
    output logic [3:0] r_port,
    output logic [3:0] g_port,
    output logic [3:0] b_port
);
    logic DE;

    VGA_Decoder U_VGA_DEC (
        .*,
        .x_pixel(),
        .y_pixel()
    );

    VGA_RGB_Switch U_VGA_RGB_SW (.*);
endmodule
```

### RTL
![alt text](<../../../assets/img/VGA/day-1/ìŠ¤í¬ë¦°ìƒ· 2025-09-08 125251.png>)